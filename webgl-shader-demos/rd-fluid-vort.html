<html>
<head>

<style>
body {
  margin: 0;
}
  #container {
    margin:0;
    position:absolute;
    width:100%;
    height:100%;
    top:0;
    z-index:-1;
  }
  .overlay {
    width: 50%;
    margin: 500px auto;
    background-color: rgba(255, 255, 255, 0.1);
    /*display:none;*/
  }

</style>


    <script src="./lib/GLOW.js" type="text/javascript" ></script>
    <script src="https://code.jquery.com/jquery-2.1.3.min.js" type="text/javascript"> </script>
    <script src="lib/stats.min.js" type="text/javascript"> </script>
    <script src="lib/dat.gui.min.js"></script>

    <script id="vs" type="x-shader/x-vertex">
      attribute   vec3  vertices;
      attribute   vec2  uvs;
      varying   vec2  uv;

      void main(void)
      {
        uv = uvs;
        gl_Position = vec4(vertices.x, vertices.y, 1., 1.0 );
      }

    </script>

    <script id="clear" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;

      void main()
      {
      
        gl_FragColor = vec4(0.0);
      }


    </script>

    <script id="sub_grad" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0;  // velocity
      uniform sampler2D   source1;  // pressure
      uniform vec2    pixelsize;

      void main()
      {
        float dx = pixelsize.x;
        float dy = pixelsize.y;
        // pressure gradient
        float U = texture2D(source1, fract(uv + vec2(0., dy))).x;
        float D = texture2D(source1, fract(uv - vec2(0., dy))).x;
        float L = texture2D(source1, fract(uv - vec2(dx, 0.))).x;
        float R = texture2D(source1, fract(uv + vec2(dx, 0.))).x;


        vec4 V = texture2D(source0, uv);
        V.xy -= 0.5 * vec2((R - L), (U - D));

        gl_FragColor = V;
      }

    </script>
    <script id="divergence" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0;
      uniform vec2    pixelsize;

      void main()
      {
        float dx = pixelsize.x;
        float dy = pixelsize.y;

        float U = texture2D(source0, fract(uv + vec2(0., dy))).y;
        float D = texture2D(source0, fract(uv - vec2(0., dy))).y;
        float L = texture2D(source0, fract(uv + vec2(dx, 0.))).x;
        float R = texture2D(source0, fract(uv - vec2(dx, 0.))).x;

        float d = - 0.5 * ((U - D) + (L - R));
        gl_FragColor = vec4(d, d, d, 1.0);
      }

    </script>
    
    <script id="advec" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0;
      uniform vec2    pixelsize;
      uniform vec2    mouse;
      uniform float     dt;

      void main()
      {
        vec2 v =  texture2D(source0, uv ).xy * pixelsize; 

        gl_FragColor =  texture2D(source0, fract(uv-v ));
      }

    </script>
    <script id="diffuse" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0;
      uniform vec2    pixelsize;
      uniform vec2    params;
      uniform float     dt;
      uniform float     time;

      void main()
      {
        float radialscale = sin(length(uv-0.5)*6.*sin(time/100.)+10.);
        float dx = pixelsize.x * (radialscale * 2. + 1.0 + sin(time/100. + 1.1) * 3.);
        float dy = pixelsize.y * (radialscale * 2. + 1.0 + sin(time/100. + 1.0) * 3.);

        vec4 U = texture2D(source0, fract(uv + vec2(0., dy)));
        vec4 D = texture2D(source0, fract(uv - vec2(0., dy)));
        vec4 L = texture2D(source0, fract(uv + vec2(dx, 0.)));
        vec4 R = texture2D(source0, fract(uv - vec2(dx, 0.)));
        vec4 C = texture2D(source0, uv);

        vec4 lapl = (U + D + L + R - 4. * C) * 0.25;

        C.xy += 0.1 *lapl.xy;
        // C += lapl;
        // GRAY SCOTT TIME
        float reaction = C.a * C.b * C.b;
        float dt = 0.9;
        float f =  0.023 + (length(uv-0.5) + sin(time/50.)) /100.;
        float k =  0.055 + (length(uv-0.5) + sin(time/50.)) /200.;
 
       C.a += dt * (0.90 * lapl.a - reaction + f * (1. - C.a));
       C.b += dt * (0.45 * lapl.b + reaction - (k + f) * C.b );

       C.ab = max(vec2(0.0), min(vec2(1.0), C.ab) );


        gl_FragColor = C;
      }
    </script>

    <script id="solve_pressure" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0; // divergence
      uniform sampler2D   source1; // pressure
      uniform sampler2D   source2; // velocity
      uniform vec2    pixelsize;
      uniform vec2    params;
      uniform float     dt;

      void main()
      {
        float dx = pixelsize.x;
        float dy = pixelsize.y;

        vec4 C = texture2D(source1, uv);
        vec4 P = texture2D(source2, uv);

        vec4 U = texture2D(source1, fract(uv + vec2(0., dy)));
        vec4 D = texture2D(source1, fract(uv - vec2(0., dy)));
        vec4 L = texture2D(source1, fract(uv + vec2(dx, 0.)));
        vec4 R = texture2D(source1, fract(uv - vec2(dx, 0.)));

        vec4 div = texture2D(source0, uv);

        // Jacobi relaxation
        vec4 lapl = (U + D + L + R + div) * 0.25 - (P.a - P.z)/100.; 

        gl_FragColor = lapl;
      }
    </script>


    <script id="vort" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0; // velocity in
      uniform vec2    pixelsize;

      uniform vec2    mouse;
      uniform float     dt;

      void main()
      {

        float dx = pixelsize.x;
        float dy = pixelsize.y;

        float U = texture2D(source0, fract(uv + vec2(0., dy))).x;
        float D = texture2D(source0, fract(uv - vec2(0., dy))).x;
        float L = texture2D(source0, fract(uv + vec2(dx, 0.))).y;
        float R = texture2D(source0, fract(uv - vec2(dx, 0.))).y;

        float curl = 0.5 * ((R - L) - (U - D));

        gl_FragColor = vec4(abs(curl));
      }
    </script>

    <script id="apply-vort" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;

      uniform sampler2D   source0; // vorticity in
      uniform sampler2D   source1; // FBO (velocity) in

      uniform vec2    pixelsize;

      uniform vec2    mouse;
      uniform float     dt;

      void main()
      {
        float dx = pixelsize.x;
        float dy = pixelsize.y;

        // get vorts
        vec4 U = texture2D(source0, fract(uv + vec2(0., dy)));
        vec4 D = texture2D(source0, fract(uv - vec2(0., dy)));
        vec4 R = texture2D(source0, fract(uv + vec2(dx, 0.)));
        vec4 L = texture2D(source0, fract(uv - vec2(dx, 0.)));    
        vec4 C = texture2D(source0, uv);    

        vec2 force_dir = vec2((R.x - L.x), (U.x - D.x));

        // float dxscale = 0.1;
          // force_dir *=  dxscale * C.x * vec2(1.,-1.);

        vec4 fluid = texture2D(source1, uv); // to the velocity

        fluid.xy += 2. * force_dir;

        gl_FragColor = fluid;
      }
    </script>

    <script id="force" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif
      varying vec2      uv;
      uniform sampler2D   source0;
      uniform sampler2D   source1; // text texture
      uniform vec2    pixelsize;

      uniform vec2    mouse;
      uniform float     time;
      uniform float     dt;

      #define TWO_PI 3.141519 * 2.
      #define PI 3.141519

      void main()
      {
        vec4 result =  texture2D(source0, uv);

        vec2 text_uv = vec2(uv.x, 1. - uv.y);
        vec4 text =  texture2D(source1, text_uv); 

        if(mouse.x > 0. && length(mouse/pixelsize - uv/pixelsize) < 10. ) {

          vec2 dir = 10. * (mouse - 0.5);
          // vec2 dir = vec2(0., 0.1);

          result.z = 1.0;
          result.a = 0.0;
          result.x = -dir.x;
          result.y = -dir.y;
        }

        gl_FragColor =  result;
      }

    </script>


    <script id="fs-render" type="x-shader/x-fragment">
      #ifdef GL_ES
        precision highp float;
      #endif

      varying vec2      uv;
      uniform sampler2D   source0;
      uniform sampler2D   source1;
      uniform sampler2D   source2;

      uniform vec2    pixelsize;
      uniform vec2    params;
      uniform float   time;

      vec3 palette(float t) {

        vec3 a = vec3(0.5, 0.5, 0.5);
        vec3 b = vec3(0.5, 0.5, 0.5); 
        vec3 c = vec3(1.0, 1.0, 1.0);
        vec3 d = vec3(0.0, 0.1, 0.2);

        return a + b*cos( 6.28318*(c*(t*3.+time/600.)+d ) );

      }

      void main()
      {
        vec4 val = texture2D(source0, uv);
        vec3 color;
        float style = 1.0;

        if( style  == 0.0 ){

          if( val.y < 0.2 ) 
            color = mix( vec3(1.,0.,0.), vec3(1.,1.,0.), val.b* 4.);
          else 
            color = mix( vec3(1.,1.,0.), vec3(0.,1.,1.), (val.b - 0.2) * 4. );

        }
        if( style  == 1.0 ){
          vec2 e = pixelsize*2.;

          // 3D SHADINGGGGG 
          float p = texture2D(source0, uv).b;
          float u = p - texture2D(source0, uv - vec2(0.0, e.y)).b ;
          float r = p - texture2D(source0, uv - vec2(e.x, 0.0)).b;

          vec3 dy = normalize(vec3(0., e.y, u/7.));
          vec3 dx = normalize(vec3(e.x ,0., r/7.));

          vec3 n = cross(dx,dy);
          float shade = dot(n, normalize(vec3(1., 0., 1.)));

          vec3 specular = vec3(1.,1.,1.) * pow(shade, 50.);

          

          color = specular + shade * palette(val.b); 

        }
        if( style  == 2.0 ) {
          vec2 e = pixelsize*5.;

          // 3D SHADINGGGGG 
          float p = texture2D(source0, uv).b;
          float u = p -texture2D(source0, uv - vec2(0.0, e.y)).b ;
          float r = p - texture2D(source0, uv - vec2(e.x, 0.0)).b;

          vec3 dy = normalize(vec3(0., e.y, u/7.));
          vec3 dx = normalize(vec3(e.x ,0., r/7.));

          vec3 n = cross(dy,dx);
          float shade = 0.1 + dot(n, normalize(vec3(0., 0., -1.)));

          color = (1. - n ) * shade * vec3( p * 3.); //vec3((atan(n.y,n.x) + PI/2. )/ PI, acos(n.z), 0.0) ;
          
        }
        
        gl_FragColor = vec4(color, 1.0);
      }
    </script>

    <script>  

      // drawing
    function setupClickUI(shader) {
      var drag = false;
      $("canvas").mousedown(function(event) {
        drag = true;
        shader.mouse.set(
          event.pageX / window.innerWidth,
          1.0 - event.pageY /window.innerHeight);
      });

      $("canvas").mouseup(function(event) {drag = false});

      $("canvas").mousemove(function(event) {
        if(drag)
          shader.mouse.set(
             event.pageX / window.innerWidth,
             1.0 - event.pageY /window.innerHeight);
      });

      $("canvas").mouseup(function(event) {
        shader.mouse.set(-1,-1);
      });
    }


    var FLUID = function(){};
    FLUID.prototype = {
      loadExtension : function(extension_name) {
        if( !this.context.enableExtension(extension_name)) {
          alert( "No support for " + extension_name + "!" );
          return;
        }
      },
  initialConditions : function(w, h, rad) {
        var noise = []
        this.zerobuffer = []
        for( var i = 0; i < w * h * 4; i ++) {
          noise.push(0);
          this.zerobuffer.push(0);
        }
        return noise;
      },
      init: function(scale) {
        var buildShader = function (frag_id, textures, uniforms) {

            var base_uniforms = {
              vertices: GLOW.Geometry.Plane.vertices(),
              uvs: GLOW.Geometry.Plane.uvs()
            };
            for( var i = 0; i < textures.length; i ++) {
              base_uniforms["source"+i] = textures[i];
            }

            $.extend(base_uniforms, uniforms);
            console.log("compiling " + frag_id)
            return new GLOW.Shader({
              data : base_uniforms,
              indices: GLOW.Geometry.Plane.indices(),
              vertexShader: document.getElementById('vs').textContent,
              fragmentShader: document.getElementById(frag_id).textContent
            });
        };

        var buildFBO = function(w, h, data) {
          return new GLOW.FBO( { 
            width: w, height: h,
              depth: false,
              data: new Float32Array( data ),
              type: GL.FLOAT,
            // minFilter: GL.NEAREST, 
            // magFilter: GL.NEAREST
          });
        };


        this.context = new GLOW.Context();
        this.container = document.getElementById( 'container' );

        this.loadExtension("OES_texture_float" );
        this.loadExtension("OES_texture_float_linear");

        this.container.appendChild(this.context.domElement );
        var w = Math.round(this.container.clientWidth / scale);
        var h = Math.round(this.container.clientHeight / scale);
        
        var noise = this.initialConditions(w, h, 100.);

        var uniforms = {
          pixelsize: new GLOW.Vector2(1 / w , 1/ h ),
          params: new GLOW.Vector2(3, 0.25),
          mouse: new GLOW.Vector2(-1,-1),
          dt: new GLOW.Float(1.),
          time: new GLOW.Float(0.),
        }


        this.text_tex = new GLOW.Texture( { 
          data: "img/title.png",
          wrap: GL.CLAMP_TO_EDGE,
        });

        // swap buffers of fluid
        this.FBO_A  = buildFBO(w, h, noise);
        this.FBO_B  = buildFBO(w, h, noise);

        // another buffer to hold pressure / divergence 
        this.FBO_div    = buildFBO(w, h, noise);
        this.FBO_pressA   = buildFBO(w, h, noise);
        this.FBO_pressB   = buildFBO(w, h, noise);

        this.renderer       = buildShader('fs-render', [this.FBO_A, this.FBO_div, this.FBO_pressA], uniforms);

        this.diffuse_b_to_a = buildShader('diffuse', [this.FBO_B], uniforms);
        this.diffuse_a_to_b = buildShader('diffuse', [this.FBO_A, this.FBO_B], uniforms);

        this.advection    = buildShader('advec', [this.FBO_B], uniforms);

        this.applyForce   = buildShader('force', [this.FBO_A, this.text_tex], uniforms);

        this.solveP_a_to_b  = buildShader('solve_pressure', [this.FBO_div, this.FBO_pressA, this.FBO_A], uniforms);
        this.solveP_b_to_a  = buildShader('solve_pressure', [this.FBO_div, this.FBO_pressB, this.FBO_B], uniforms);

        this.projectField   = buildShader('sub_grad', [this.FBO_A, this.FBO_pressA], uniforms);

        this.calcDivergence = buildShader('divergence', [this.FBO_A], uniforms);

        this.calc_vort    = buildShader('vort', [this.FBO_B], uniforms);
        this.apply_vort     = buildShader('apply-vort', [this.FBO_div, this.FBO_B], uniforms);

        this.clearFBO   = buildShader('clear', [], uniforms);
      setupClickUI(this.applyForce);

      },
      shaderPass: function(shader, tgt) {
        this.context.cache.clear();
        tgt.bind();
        shader.draw();
        tgt.unbind();
      },
      render : function() {

        // A -> B UI input
        this.shaderPass(this.applyForce, this.FBO_B);
        this.diffuse_a_to_b.time.add(1);

        // B -> A advection
        this.shaderPass(this.advection, this.FBO_A);

        // relaxation to solve diffusion eq - TODO use seperable gaussian?
          // little bit of diffusion
        for ( var i = 0; i < 3; i ++ ) {
          this.shaderPass(this.diffuse_a_to_b, this.FBO_B); 
          this.shaderPass(this.diffuse_b_to_a, this.FBO_A);
        }
        // A -> div
        this.shaderPass(this.calcDivergence, this.FBO_div);
        
        // use the same relaxation to solve for pressure
        // div + press -> press
        // this.shaderPass(this.clearFBO, this.FBO_pressA);

        for ( var i = 0; i <  8; i ++ ) {
          this.shaderPass(this.solveP_a_to_b, this.FBO_pressB);
          this.shaderPass(this.solveP_b_to_a, this.FBO_pressA);
        }

        // project field onto zero divergence 
        this.shaderPass(this.projectField, this.FBO_B);
        this.shaderPass(this.calc_vort, this.FBO_div);
        this.shaderPass(this.apply_vort, this.FBO_A);
        // A-> Screen
        this.renderer.draw();
      }
    };

    $(document).ready(function() {
      // STATS INIT
      ritika = new Stats();
      ritika.domElement.style.position = 'absolute';
      ritika.domElement.style.bottom = '0px';
      container.appendChild( ritika.domElement );

      // FLUID SIM
      sim = new FLUID();
      sim.init(3);
      function animate() {
        ritika.update();
        sim.render();
        requestAnimationFrame(animate);
      }
      
      animate();
    });

    

    </script>




</head>

<body id="container">
<!--   <div > </div>
  <div class="overlay"> 
    <h1>WebGL Demos </h1>
  </div> -->
</body >



<script type="text/javascript">



$( document ).ready(function() {



});
</script>

</html>
<!--  -->